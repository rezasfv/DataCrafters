#List of the best attackers (high number of xG90) with a low market value at the end of a specific season

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dcsso: <http://www.dei.unipd.it/db2/dcsso#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?playerName ?teamName ?subPosition ?xG90 ?value ?games WHERE 
{
	?p foaf:name ?playerName;
		dcsso:hasMembership ?m;
		dcsso:position ?position;
		dcsso:subPosition ?subPosition.
    
	?m dcsso:season "2018"^^xsd:int;
		dcsso:forTeam/foaf:name ?teamName;
		dcsso:xG90 ?xG90;
		dcsso:tmValue ?value;
		dcsso:games ?games.
    
	FILTER(!EXISTS
	{
		?m a dcsso:SeasonalMembership.
		
		?p dcsso:hasMembership ?m2.
		
		?m2 a dcsso:SeasonalAggrMembership;
			dcsso:season "2018"^^xsd:int.
	}) 
	FILTER (?xG90 > "0.55"^^xsd:double).
	FILTER (?value < "20000000"^^xsd:int).
	FILTER (?position = dcsso:Forward).
	FILTER (?games > "15"^^xsd:int).
}
LIMIT 20